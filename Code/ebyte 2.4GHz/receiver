#include <Wire.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>

#define UART_PORT_NUM     1
#define UART_BAUD         9600
#define UART_RX_PIN       16   // from radio
#define UART_TX_PIN       17   // to radio

#define I2C_SDA_PIN       21
#define I2C_SCL_PIN       22
#define OLED_ADDR         0x3C
#define SCREEN_WIDTH      128
#define SCREEN_HEIGHT     64
#define OLED_RESET        -1

#define CHAR_W            6
#define CHAR_H            8
#define MAX_COLS          (SCREEN_WIDTH / CHAR_W)
#define MAX_LINES         (SCREEN_HEIGHT / CHAR_H)

Adafruit_SSD1306 display(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, OLED_RESET);
HardwareSerial ExtSerial(UART_PORT_NUM);

String linesBuf[MAX_LINES];
String currentLine;
bool dirty = true;

void pushLine(const String& s){ for (int i=0;i<MAX_LINES-1;i++) linesBuf[i]=linesBuf[i+1]; linesBuf[MAX_LINES-1]=s; dirty=true; }
void drawScreen(){
  if(!dirty) return;
  display.clearDisplay(); display.setTextSize(1); display.setTextColor(SSD1306_WHITE); display.setCursor(0,0);
  for(int i=0;i<MAX_LINES-1;i++) display.println(linesBuf[i]);
  display.println(currentLine); display.display(); dirty=false;
}
void addChar(char c){
  if(c=='\r') return;
  if(c=='\n'){ pushLine(currentLine); currentLine=""; return; }
  currentLine += c;
  if ((int)currentLine.length() >= MAX_COLS) { pushLine(currentLine); currentLine=""; }
  dirty = true;
}

void setup(){
  Serial.begin(9600);
  ExtSerial.begin(UART_BAUD, SERIAL_8N1, UART_RX_PIN, UART_TX_PIN);

  Wire.begin(I2C_SDA_PIN, I2C_SCL_PIN);
  if(!display.begin(SSD1306_SWITCHCAPVCC, OLED_ADDR)){ Serial.println("SSD1306 init failed"); }
  else{
    display.clearDisplay(); display.setTextSize(1); display.setTextColor(SSD1306_WHITE); display.setCursor(0,0);
    display.println("USB<->UART Bridge"); display.println("@9600 baud"); display.display(); delay(600);
    display.clearDisplay(); display.display();
  }
  for(int i=0;i<MAX_LINES;i++) linesBuf[i]="";
  currentLine.reserve(MAX_COLS+4);
}

void loop(){
  while(ExtSerial.available()>0){ char c=(char)ExtSerial.read(); Serial.write(c); addChar(c); }
  while(Serial.available()>0){    char c=(char)Serial.read();    ExtSerial.write(c); }
  drawScreen();
}
